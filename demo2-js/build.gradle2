
buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.moowork.gradle:gradle-node-plugin:1.2.0"
    }
}

apply plugin: 'kotlin-platform-js'
apply plugin: 'com.moowork.node'

compileKotlin2Js {
    kotlinOptions {
        moduleKind = "umd"
        sourceMap = true
        metaInfo = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    expectedBy project(":demo")
    compile project(':othermod-js')  // TODO: needed? that would suck
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
}

node {
    download = true
    version = '9.4.0'
    npmVersion = '5.6.0'
    nodeModulesDir = file("${buildDir}")
}

/* Install the Kotlin javascript file. */
task installKotlin(type: NpmTask) {
  args = ['install', '--silent', 'kotlin']
}

assemble.dependsOn installKotlin

task run(type: NodeTask) {
    description = "Run the Javascript code using NodeJS (no browser)"
    dependsOn build
    script = file("${rootProject.buildDir.path}/classes/main/demo-js.js")
}


